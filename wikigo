#!/usr/bin/env perl
use strict;
use warnings;
use utf8;

use Plack::Builder;
use Plack::Runner;
use Plack::App::Directory;
use File::Spec;

my $root = @ARGV ? shift : '.';

# Default differently than plackup's port 5000 so we don't conflict.
unshift @ARGV, '--port' => 4242;

my $app = builder {
    my @markdown = qw(md mkdwn markdown);
    my $extensions = join "|", @markdown;
    my $is_markdown = qr/\.($extensions)$/i;

    # Don't require the extension for markdown files
    enable Rewrite =>
        rules => sub {
            my $path = File::Spec->rel2abs(substr($_,1), $root);
            return if -e $path;
            for my $ext (@markdown) {
                if (-f "$path.$ext") {
                    $_ .= ".$ext";
                    last;
                }
            }
            return;
        };

    # Render markdown â†’ HTML
    enable_if { $_[0]->{PATH_INFO} =~ /$is_markdown/ }
        SimpleContentFilter => filter => sub {
            eval {
                require Text::MultiMarkdown;
                $_ = Text::MultiMarkdown::markdown($_, { use_wikilinks => 1 });
            };
            return $_;
        };

    enable_if { $_[0]->{PATH_INFO} =~ /$is_markdown/ }
        Header => set => [ 'Content-Type' => 'text/html' ];

    # Always show the latest and greatest
    enable Expires =>
        content_type => qr/./,
        expires      => "now plus 1 second";

    Plack::App::Directory->new({ root => $root })->to_app;
};

my $runner = Plack::Runner->new;
$runner->parse_options(@ARGV);
$runner->run($app);
